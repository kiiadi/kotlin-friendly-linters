import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
    ext.kotlinVersion = "$kotlinVersion"

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokkaVersion"
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
        classpath 'net.researchgate:gradle-release:2.6.0'
    }
}

group = "com.kiiadi.ktfriendly"

apply plugin: 'net.researchgate.release'

release {
    failOnPublishNeeded = false
    buildTasks = ['uploadArchives']
    preTagCommitMessage = '[Release] Updating to release version:'
    newVersionCommitMessage = '[Release] Updating to new snapshot version:'
}

allprojects {
    apply plugin: 'com.bmuschko.nexus'

    modifyPom {
        project {
            url 'https://github.com/kiiadi/kotlin-friendly-linters'

            scm {
                url 'https://github.com/kiiadi/kotlin-friendly-linters'
                connection 'scm:https://kiiadi@github.com/kiiadi/kotlin-friendly-linters.git'
                developerConnection 'scm:git://github.com/kiiadi/kotlin-friendly-linters.git'
            }

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'kiiadi'
                    name 'Kyle Thomson'
                }
            }
        }
    }
}

subprojects {
    repositories {
        mavenCentral()
    }

    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'org.jetbrains.dokka'
    apply plugin: 'maven'
    apply plugin: 'jacoco'

    group = parent.group
    version = parent.version

    sourceSets {
        main.java.srcDir 'src'
        main.resources.srcDir 'resources'
        test.java.srcDir 'tst'
        test.resources.srcDir 'tst-resources'
    }

    project.parent.tasks.findByName('uploadArchives').dependsOn(uploadArchives)

    jacoco {
        toolVersion = "0.8.2"
    }

    jacocoTestReport {
        reports {
            xml.enabled true
            
        }
    }

    dokka {
        outputFormat = 'javadoc'
        outputDirectory = "$buildDir/javadoc"
    }

    task dokkaJavadocJar(type: Jar) {
        classifier = 'javadoc'
        group = BasePlugin.BUILD_GROUP
        description = 'Assembles a jar archive containing the generated Javadoc API documentation of this project.'
        from dokka
    }

    artifacts {
        archives dokkaJavadocJar
    }

    extraArchive {
        javadoc = false
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
        testCompile "junit:junit:4.12"
    }

    tasks.withType(KotlinCompile).all {
        kotlinOptions.jvmTarget = "1.8"
    }
}